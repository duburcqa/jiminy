# Minimum version required
cmake_minimum_required(VERSION 3.10)

# Set the build version
set(BUILD_VERSION 1.7.13)

# Set compatibility
if(CMAKE_VERSION VERSION_GREATER "3.11.0")
    set(COMPATIBILITY_VERSION SameMinorVersion)
else()
    set(COMPATIBILITY_VERSION ExactVersion)
endif()

# Extract major, minor and patch version
string(REPLACE "." ";" _VERSION "${BUILD_VERSION}")
list(GET _VERSION 0 BUILD_VERSION_MAJOR)
list(GET _VERSION 1 BUILD_VERSION_MINOR)
list(GET _VERSION 2 BUILD_VERSION_PATCH)

# Add definition of Jiminy version for C++ headers
add_definitions("-DJIMINY_VERSION=\"${BUILD_VERSION}\"")

# Enable C++ language
enable_language(CXX)

# Project and library name
project(jiminy VERSION ${BUILD_VERSION})
set(LIBRARY_NAME ${PROJECT_NAME})
set(PYTHON_LIBRARY_NAME "core")

# Set build environment and standard dependencies
include(${CMAKE_SOURCE_DIR}/build_tools/cmake/base.cmake)
include(${CMAKE_SOURCE_DIR}/build_tools/cmake/docs.cmake)
include(${CMAKE_SOURCE_DIR}/build_tools/cmake/boostPythonDocstring.cmake)
include(${CMAKE_SOURCE_DIR}/build_tools/cmake/exportCmakeConfigFiles.cmake)
include(${CMAKE_SOURCE_DIR}/build_tools/cmake/buildPythonWheel.cmake)

# Set the compilation flags.
# Note that is is not possible `add_compile_options`
# because it does not propagate to external projects.
set(CMAKE_CXX_FLAGS "$ENV{CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS}")
if(WIN32)
    string(REGEX MATCH "/arch:[A-Za-z_0-9/-]*" CMAKE_CXX_ARCH "${CMAKE_CXX_FLAGS}")
    if(CMAKE_CXX_ARCH MATCHES "AVX[0-9]+")
        # Eigen<3.4 relies on __FMA__ definition to detect if FMA is available,
        # which is not set automatically by MSVC.
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -D__FMA__")
        if(MSVC_VERSION GREATER 1930)
            # Floating-point contractions are not generated by default on MSVC > 2019.
            # After this point, it is necessary to pass `/fp:contract`. Following the
            # same paradigm as Eigen>3.4, we enable it if AVX2+ is required.
            set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /fp:contract")
        endif()
    endif()
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /EHsc /bigobj /Zc:__cplusplus /permissive- -DWIN32 -D_USE_MATH_DEFINES -DNOMINMAX")
    set(CMAKE_CXX_FLAGS_DEBUG "/DEBUG:FULL /Zi /Od")
    set(CMAKE_CXX_FLAGS_RELEASE "-DNDEBUG /O2 /Ob3")
    set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELEASE} /DEBUG:FULL /Zi")
else()
    string(REGEX MATCH "-march=[A-Za-z_0-9/-]*" CMAKE_CXX_ARCH "${CMAKE_CXX_FLAGS}")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
    set(CMAKE_CXX_FLAGS_DEBUG "-g -O0 -ftemplate-backtrace-limit=0")
    set(CMAKE_CXX_FLAGS_RELEASE "-DNDEBUG -O3 -Wfatal-errors -Werror")
    set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELEASE} -g -fno-omit-frame-pointer -fno-sanitize-recover")
endif()

# Sub-projects
add_subdirectory(soup)
add_subdirectory(core)

option(BUILD_PYTHON_INTERFACE "Build and install the Python packages." ON)
if(BUILD_PYTHON_INTERFACE)
    include(${CMAKE_SOURCE_DIR}/build_tools/cmake/setupPython.cmake)
    add_subdirectory(python)
endif()

option(BUILD_EXAMPLES "Build the C++ examples." ON)
if(BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()

option(BUILD_TESTING "Build the C++ unit tests." ON)
if(BUILD_TESTING)
    include(CTest)
    include(GoogleTest)
    add_subdirectory(unit)
endif()

# Create 'docs' optional cmake component to generate documentation.
create_component_docs()

# Install the source cmake files
file(GLOB_RECURSE SOURCE_CMAKE_FILES "${CMAKE_SOURCE_DIR}/build_tools/cmake/Find*.cmake")
install(FILES ${SOURCE_CMAKE_FILES}
        DESTINATION "${CMAKE_INSTALL_DATADIR}/${LIBRARY_NAME}/cmake"
)
install(FILES ${CMAKE_SOURCE_DIR}/build_tools/cmake/base.cmake
        DESTINATION "${CMAKE_INSTALL_DATADIR}/${LIBRARY_NAME}/cmake"
        RENAME ${LIBRARY_NAME}_base.cmake
)
install(FILES ${CMAKE_SOURCE_DIR}/build_tools/cmake/boostPythonDocstring.cmake
        DESTINATION "${CMAKE_INSTALL_DATADIR}/${LIBRARY_NAME}/cmake"
)
